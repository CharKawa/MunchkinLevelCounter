buildscript {
	repositories {
		maven { url 'https://maven.fabric.io/public' }
	}
	dependencies {
		classpath 'io.fabric.tools:gradle:1.22.1'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'android-apt'
apply plugin: 'io.fabric'

android {
	signingConfigs {
	}
	compileSdkVersion 25
	buildToolsVersion '23.0.3'
	defaultConfig {
		applicationId 'com.g_art.munchkinlevelcounter'
		minSdkVersion 15
		targetSdkVersion 25
		versionCode 30
		versionName '1.2'
		vectorDrawables.useSupportLibrary = true

		applicationVariants.all { variant ->
			variant.outputs.each { output ->
				def file = output.outputFile
				def fileName = file.name.replace(".apk", "-v" + versionName + "-c" + versionCode + ".apk")
				output.outputFile = new File(file.parentFile, fileName)
			}
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			minifyEnabled false
		}
	}
	productFlavors {
		main {
		}
		free {
			buildConfigField "boolean", "USE_FABRIC", "false"
			ext.enableCrashlytics = false
		}
		paid {
			applicationId 'com.g_art.munchkinlevelcounter.paid'

			buildConfigField "boolean", "USE_FABRIC", "false"
			ext.enableCrashlytics = false
		}
		amazon {
			applicationId 'com.g_art.munchkinlevelcounter.paid.underground'

			buildConfigField "boolean", "USE_FABRIC", "true"
		}
	}

	lintOptions {
		disable 'MissingTranslation'
	}
}
repositories {
	jcenter()
	maven { url "https://jitpack.io" }
	maven { url 'https://maven.fabric.io/public' }
}

dependencies {
//	compile fileTree(include: ['*.jar'], dir: 'libs')
	compile 'com.android.support:support-v4:25.1.1'
	compile 'com.android.support:recyclerview-v7:25.1.1'
	compile 'com.android.support:design:25.1.1'
	compile 'com.android.support:appcompat-v7:25.1.1'
	compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
	compile 'com.google.code.gson:gson:2.4'

	//Utils
	//Line Charts
	compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
	//View injection
	compile 'com.jakewharton:butterknife:8.2.1'
	apt 'com.jakewharton:butterknife-compiler:8.2.1'
	//Font
	compile 'uk.co.chrisjenx:calligraphy:2.2.0'
	//Material Dialog
	compile 'com.afollestad.material-dialogs:core:0.9.3.0'
	compile 'com.afollestad.material-dialogs:commons:0.9.3.0'

	//Analytics
	//Fabric
	amazonCompile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
		transitive = true;
	}
	//For Paid version
	paidCompile 'com.google.android.gms:play-services-analytics:8.1.0'
	//For Free version
	freeCompile 'com.google.android.gms:play-services-analytics:8.1.0'
}
